plugins {
    id 'net.ltgt.errorprone' version '0.0.14'
}
apply plugin: 'com.android.application'
apply plugin: 'com.novoda.android-command'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'com.novoda.static-analysis'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'

buildProperties {
    signing.using(file('props/release.signing')) | file('props/debug.signing')
    secrets.using(file('props/secrets.properties'))
}
def secrets = buildProperties.secrets

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.tasomaniac.muzei.earthview"
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        resConfigs "en"
    }

    signingConfigs {
        debug.storeFile file('props/debug.keystore')
        release {
            def signing = buildProperties.signing
            storeFile file("props/${signing['storeFile'].string}")
            storePassword signing['storePassword'].string
            keyAlias signing['keyAlias'].string
            keyPassword signing['keyPassword'].string
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

staticAnalysis {
    penalty {
        maxWarnings = 0
        maxErrors = 0
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        checkAllWarnings true
        checkReleaseBuilds false

        lintConfig file('props/lint.xml')

        includeVariants { it.name == 'debug' }
    }
}

play {
    serviceAccountEmail = secrets['play_service_account'].or('').string
    pk12File = file(secrets['play_key_file'].or('no_file.p12').string)
    track = 'alpha'
}

dependencies {
    def retrofitVersion = '2.4.0'

    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:preference-v14:$rootProject.ext.supportLibVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibVersion"

    compile 'com.jakewharton.timber:timber:4.7.1'

    compile 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'

    compile 'com.google.android.apps.muzei:muzei-api:3.0.0-alpha01'
//    compile project(':api')

    compile 'com.tasomaniac:integrationpreference-support:0.2'
    compile 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    testCompile 'junit:junit:4.12'
}
